lang	:-	statement*;

statement	:-	block
			|	declaration
			|	cycle_statement
			|	selection_statement
			|	jump_statements
			|	arifmetic_expression_list
			|	SEMICOLON
			;

block 	:-	L_FIG statement* R_FIG
		;
 
declaration	:-	variable_declaration
			|	function_declaration
			;

variable_declaration	:-	declarator (STRONG_ASSIGN_OP arifmetic_expression)? extended_declaration*
						;

declarator	:-	(TYPE_SPECIFIER | CLASS) array_declatation_part? IDENTIFIER
			;

array_declatation_part	:-	L_SQR INT R_SQR
						;

function_declaration	:-	DEF IDENTIFIER L_RND attributes_list? R_RND after_keyword
						;

attributes_list	:-	attribute (COMMA attribute)*
				;

attribute	:-	TYPE_SPECIFIER POINTER* IDENTIFIER
			;

extended_declaration	:-	COMMA IDENTIFIER (STRONG_ASSIGN_OP arifmetic_expression)?
						;

cycle_statement	:-	FOR for_condition_expr after_keyword
				|	WHILE condition_expr after_keyword
				|	// DO after_keyword WHILE condition_expr ?
				;

for_condition_expr	:-	L_RND (variable_declaration | arifmetic_expression_list)? SEMICOLON condition? SEMICOLON arifmetic_expression_list? R_RND
					;

selection_statement	:-	IF condition_expr after_keyword (ELIF condition_expr after_keyword)* (ELSE after_keyword)?
					|	// SELECT ?
					;

after_keyword	:-	block
				|	statement
				;

jump_statements	:-	RETURN
				|	BREAK
				|	CONTINUE
				;

condition_expr	:-	L_RND condition R_RND
				;

condition	:-	arifmetic_expression
			;

arifmetic_expression_list	:-	arifmetic_expression (COMMA arifmetic_expression)*
							;

arifmetic_expression	:-	unary_expression (BINAR_OP arifmetic_expression)*
						;

unary_expression	:-	OP_UNAR_PREF unary_expression
					|	OP_UNAR_BINAR unary_expression
					|	postfix_expression postfix_operations*
					;

postfix_expression	:-	literal
					|	L_RND arifmetic_expression R_RND
					;

postfix_operations	:-	L_RND arifmetic_expression_list? R_RND
					|	L_SQR arifmetic_expression R_SQR
					|	OP_UNAR_POST
					;

literal	:-	IDENTIFIER
		|	STRING
		|	INT
		|	FLOAT
		;













